apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

// Manifest version information
def versionMajor = 0
def versionMinor = 4
def versionPatch = 0

ext {
    bintrayRepo = 'maven'
    bintrayName = 'blueteeth'

    publishedGroupId = 'com.robotpajamas.blueteeth'
    libraryName = 'Blueteeth'
    artifact = 'blueteeth'

    libraryDescription = 'An Android Bluetooth library'

    siteUrl = 'https://github.com/RobotPajamas/Blueteeth'
    gitUrl = 'https://github.com/RobotPajamas/Blueteeth.git'

    libraryVersion = "${versionMajor}.${versionMinor}.${versionPatch}-KOTSHOT"

    developerId = 'sureshjoshi'
    developerName = 'Suresh Joshi'
    developerEmail = 'suresh@robotpajamas.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 22
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    api "com.robotpajamas.dispatcher:dispatcher:0.3.0"
    testImplementation 'junit:junit:4.12'
}

if (project.rootProject.file('local.properties').exists()) {
    apply from: 'https://raw.githubusercontent.com/sureshjoshi/JCenter/master/installv1.gradle'
    apply from: 'https://raw.githubusercontent.com/sureshjoshi/JCenter/master/bintrayv1.gradle'
    // If there are issues from Github, then use the local files
    // apply from: '../jcenter/installv1.gradle'
    // apply from: '../jcenter/bintrayv1.gradle'
}

tasks.withType(Javadoc) {
    enabled = false
}
